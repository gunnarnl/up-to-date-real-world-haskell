<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" lang="" xml:lang="">
<head>
  <meta charset="utf-8" />
  <meta name="generator" content="pandoc" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0, user-scalable=yes" />
  <title>Chapter 27. Sockets and Syslog</title>
  <style>
    html {
      line-height: 1.5;
      font-family: Georgia, serif;
      font-size: 20px;
      color: #1a1a1a;
      background-color: #fdfdfd;
    }
    body {
      margin: 0 auto;
      max-width: 36em;
      padding-left: 50px;
      padding-right: 50px;
      padding-top: 50px;
      padding-bottom: 50px;
      hyphens: auto;
      overflow-wrap: break-word;
      text-rendering: optimizeLegibility;
      font-kerning: normal;
    }
    @media (max-width: 600px) {
      body {
        font-size: 0.9em;
        padding: 1em;
      }
      h1 {
        font-size: 1.8em;
      }
    }
    @media print {
      body {
        background-color: transparent;
        color: black;
        font-size: 12pt;
      }
      p, h2, h3 {
        orphans: 3;
        widows: 3;
      }
      h2, h3, h4 {
        page-break-after: avoid;
      }
    }
    p {
      margin: 1em 0;
    }
    a {
      color: #1a1a1a;
    }
    a:visited {
      color: #1a1a1a;
    }
    img {
      max-width: 100%;
    }
    h1, h2, h3, h4, h5, h6 {
      margin-top: 1.4em;
    }
    h5, h6 {
      font-size: 1em;
      font-style: italic;
    }
    h6 {
      font-weight: normal;
    }
    ol, ul {
      padding-left: 1.7em;
      margin-top: 1em;
    }
    li > ol, li > ul {
      margin-top: 0;
    }
    blockquote {
      margin: 1em 0 1em 1.7em;
      padding-left: 1em;
      border-left: 2px solid #e6e6e6;
      color: #606060;
    }
    code {
      font-family: Menlo, Monaco, 'Lucida Console', Consolas, monospace;
      font-size: 85%;
      margin: 0;
    }
    pre {
      margin: 1em 0;
      overflow: auto;
    }
    pre code {
      padding: 0;
      overflow: visible;
      overflow-wrap: normal;
    }
    .sourceCode {
     background-color: transparent;
     overflow: visible;
    }
    hr {
      background-color: #1a1a1a;
      border: none;
      height: 1px;
      margin: 1em 0;
    }
    table {
      margin: 1em 0;
      border-collapse: collapse;
      width: 100%;
      overflow-x: auto;
      display: block;
      font-variant-numeric: lining-nums tabular-nums;
    }
    table caption {
      margin-bottom: 0.75em;
    }
    tbody {
      margin-top: 0.5em;
      border-top: 1px solid #1a1a1a;
      border-bottom: 1px solid #1a1a1a;
    }
    th {
      border-top: 1px solid #1a1a1a;
      padding: 0.25em 0.5em 0.25em 0.5em;
    }
    td {
      padding: 0.125em 0.5em 0.25em 0.5em;
    }
    header {
      margin-bottom: 4em;
      text-align: center;
    }
    #TOC li {
      list-style: none;
    }
    #TOC ul {
      padding-left: 1.3em;
    }
    #TOC > ul {
      padding-left: 0;
    }
    #TOC a:not(:hover) {
      text-decoration: none;
    }
    code{white-space: pre-wrap;}
    span.smallcaps{font-variant: small-caps;}
    span.underline{text-decoration: underline;}
    div.column{display: inline-block; vertical-align: top; width: 50%;}
    div.hanging-indent{margin-left: 1.5em; text-indent: -1.5em;}
    ul.task-list{list-style: none;}
    .display.math{display: block; text-align: center; margin: 0.5rem auto;}
  </style>
  <!--[if lt IE 9]>
    <script src="//cdnjs.cloudflare.com/ajax/libs/html5shiv/3.7.3/html5shiv-printshiv.min.js"></script>
  <![endif]-->
</head>
<body>
<header id="title-block-header">
<h1 class="title">Chapter 27. Sockets and Syslog</h1>
</header>
<nav id="TOC" role="doc-toc">
<ul>
<li><a href="#basic-networking"><span
class="toc-section-number">1</span> Basic Networking</a></li>
<li><a href="#communicating-with-udp"><span
class="toc-section-number">2</span> Communicating with UDP</a>
<ul>
<li><a href="#udp-client-example-syslog"><span
class="toc-section-number">2.1</span> UDP Client Example:
syslog</a></li>
<li><a href="#udp-syslog-server"><span
class="toc-section-number">2.2</span> UDP Syslog Server</a></li>
</ul></li>
<li><a href="#communicating-with-tcp"><span
class="toc-section-number">3</span> Communicating with TCP</a>
<ul>
<li><a href="#handling-multiple-tcp-streams"><span
class="toc-section-number">3.1</span> Handling Multiple TCP
Streams</a></li>
<li><a href="#tcp-syslog-server"><span
class="toc-section-number">3.2</span> TCP Syslog Server</a></li>
<li><a href="#tcp-syslog-client"><span
class="toc-section-number">3.3</span> TCP Syslog Client</a></li>
</ul></li>
</ul>
</nav>
<h1 data-number="1" id="basic-networking"><span
class="header-section-number">1</span> Basic Networking</h1>
<p>In several earlier chapters of this book, we have discussed services
that operate over a network. Two examples are client/server databases
and web services. When the need arises to devise a new protocol, or to
communicate with a protocol that doesn't have an existing helper library
in Haskell, you'll need to use the lower-level networking tools in the
Haskell library.</p>
<p>In this chapter, we will discuss these lower-level tools. Network
communication is a broad topic with entire books devoted to it. In this
chapter, we will show you how to use Haskell to apply low-level network
knowledge you already have.</p>
<p>Haskell's networking functions almost always correspond directly to
familiar C function calls. As most other languages also layer atop C,
you should find this interface familiar.</p>
<h1 data-number="2" id="communicating-with-udp"><span
class="header-section-number">2</span> Communicating with UDP</h1>
<p>UDP breaks data down into packets. It does not ensure that the data
reaches its destination, or reaches it only once. It does use
checksumming to ensure that packets that arrive have not been corrupted.
UDP tends to be used in applications that are performance- or
latency-sensitive, in which each individual packet of data is less
important than the overall performance of the system. It may also be
used where the TCP behavior isn't the most efficient, such as ones that
send short, discrete messages. Examples of systems that tend to use UDP
include audio and video conferencing, time synchronization,
network-based filesystems, and logging systems.</p>
<h2 data-number="2.1" id="udp-client-example-syslog"><span
class="header-section-number">2.1</span> UDP Client Example: syslog</h2>
<p>The traditional Unix syslog service allows programs to send log
messages over a network to a central server that records them. Some
programs are quite performance-sensitive, and may generate a large
volume of messages. In these programs, it could be more important to
have the logging impose a minimal performance overhead than to guarantee
every message is logged. Moreover, it may be desirable to continue
program operation even if the logging server is unreachable. For this
reason, UDP is one of the protocols supported by syslog for the
transmission of log messages. The protocol is simple and we present a
Haskell implementation of a client here.</p>
<pre class="example"><code>import Data.Bits
import Network.Socket
import Network.BSD
import Data.List
import SyslogTypes

data SyslogHandle =
    SyslogHandle {slSocket :: Socket,
                  slProgram :: String,
                  slAddress :: SockAddr}

openlog :: HostName             -- ^ Remote hostname, or localhost
        -&gt; String               -- ^ Port number or name; 514 is default
        -&gt; String               -- ^ Name to log under
        -&gt; IO SyslogHandle      -- ^ Handle to use for logging
openlog hostname port progname =
    do -- Look up the hostname and port.  Either raises an exception
       -- or returns a nonempty list.  First element in that list
       -- is supposed to be the best option.
       addrinfos &lt;- getAddrInfo Nothing (Just hostname) (Just port)
       let serveraddr = head addrinfos

       -- Establish a socket for communication
       sock &lt;- socket (addrFamily serveraddr) Datagram defaultProtocol

       -- Save off the socket, program name, and server address in a handle
       return $ SyslogHandle sock progname (addrAddress serveraddr)

syslog :: SyslogHandle -&gt; Facility -&gt; Priority -&gt; String -&gt; IO ()
syslog syslogh fac pri msg =
    sendstr sendmsg
    where code = makeCode fac pri
          sendmsg = &quot;&lt;&quot; ++ show code ++ &quot;&gt;&quot; ++ (slProgram syslogh) ++
                    &quot;: &quot; ++ msg

          -- Send until everything is done
          sendstr :: String -&gt; IO ()
          sendstr [] = return ()
          sendstr omsg = do sent &lt;- sendTo (slSocket syslogh) omsg
                                    (slAddress syslogh)
                            sendstr (genericDrop sent omsg)

closelog :: SyslogHandle -&gt; IO ()
closelog syslogh = sClose (slSocket syslogh)

{- | Convert a facility and a priority into a syslog code -}
makeCode :: Facility -&gt; Priority -&gt; Int
makeCode fac pri =
    let faccode = codeOfFac fac
        pricode = fromEnum pri
        in
          (faccode `shiftL` 3) .|. pricode
</code></pre>
<p>This also requires <code>SyslogTypes.hs</code>, shown here:</p>
<pre class="example"><code>module SyslogTypes where
{- | Priorities define how important a log message is. -}

data Priority =
            DEBUG                   -- ^ Debug messages
          | INFO                    -- ^ Information
          | NOTICE                  -- ^ Normal runtime conditions
          | WARNING                 -- ^ General Warnings
          | ERROR                   -- ^ General Errors
          | CRITICAL                -- ^ Severe situations
          | ALERT                   -- ^ Take immediate action
          | EMERGENCY               -- ^ System is unusable
                    deriving (Eq, Ord, Show, Read, Enum)

{- | Facilities are used by the system to determine where messages
are sent. -}

data Facility =
              KERN                      -- ^ Kernel messages
              | USER                    -- ^ General userland messages
              | MAIL                    -- ^ E-Mail system
              | DAEMON                  -- ^ Daemon (server process) messages
              | AUTH                    -- ^ Authentication or security messages
              | SYSLOG                  -- ^ Internal syslog messages
              | LPR                     -- ^ Printer messages
              | NEWS                    -- ^ Usenet news
              | UUCP                    -- ^ UUCP messages
              | CRON                    -- ^ Cron messages
              | AUTHPRIV                -- ^ Private authentication messages
              | FTP                     -- ^ FTP messages
              | LOCAL0
              | LOCAL1
              | LOCAL2
              | LOCAL3
              | LOCAL4
              | LOCAL5
              | LOCAL6
              | LOCAL7
                deriving (Eq, Show, Read)

facToCode = [
                       (KERN, 0),
                       (USER, 1),
                       (MAIL, 2),
                       (DAEMON, 3),
                       (AUTH, 4),
                       (SYSLOG, 5),
                       (LPR, 6),
                       (NEWS, 7),
                       (UUCP, 8),
                       (CRON, 9),
                       (AUTHPRIV, 10),
                       (FTP, 11),
                       (LOCAL0, 16),
                       (LOCAL1, 17),
                       (LOCAL2, 18),
                       (LOCAL3, 19),
                       (LOCAL4, 20),
                       (LOCAL5, 21),
                       (LOCAL6, 22),
                       (LOCAL7, 23)
           ]

codeToFac = map (\(x, y) -&gt; (y, x)) facToCode

{- | We can&#39;t use enum here because the numbering is discontiguous -}
codeOfFac :: Facility -&gt; Int
codeOfFac f = case lookup f facToCode of
                Just x -&gt; x
                _ -&gt; error $ &quot;Internal error in codeOfFac&quot;

facOfCode :: Int -&gt; Facility
facOfCode f = case lookup f codeToFac of
                Just x -&gt; x
                _ -&gt; error $ &quot;Invalid code in facOfCode&quot;
</code></pre>
<p>With <code>ghci</code>, you can send a message to a local syslog
server. You can use either the example syslog server presented in this
chapter, or an existing syslog server like you would typically find on
Linux or other POSIX systems. Note that most of these disable the UDP
port by default and you may need to enable UDP before your
vendor-supplied syslog daemon will display received messages.</p>
<p>If you were sending a message to a syslog server on the local system,
you might use a command such as this:</p>
<pre class="screen"><code>ghci&gt; :load syslogclient.hs
[1 of 2] Compiling SyslogTypes      ( SyslogTypes.hs, interpreted )
[2 of 2] Compiling Main             ( syslogclient.hs, interpreted )
Ok, modules loaded: SyslogTypes, Main.
ghci&gt; h &lt;- openlog &quot;localhost&quot; &quot;514&quot; &quot;testprog&quot;
Loading package parsec-2.1.0.0 ... linking ... done.
Loading package network-2.1.0.0 ... linking ... done.
ghci&gt; syslog h USER INFO &quot;This is my message&quot;
ghci&gt; closelog h
</code></pre>
<h2 data-number="2.2" id="udp-syslog-server"><span
class="header-section-number">2.2</span> UDP Syslog Server</h2>
<p>UDP servers will bind to a specific port on the server machine. They
will accept packets directed to that port and process them. Since UDP is
a stateless, packet-oriented protocol, programmers normally use a call
such as <code>recvFrom</code> to receive both the data and information
about the machine that sent it, which is used for sending back a
response.</p>
<pre class="example"><code>import Data.Bits
import Network.Socket
import Network.BSD
import Data.List

type HandlerFunc = SockAddr -&gt; String -&gt; IO ()

serveLog :: String              -- ^ Port number or name; 514 is default
         -&gt; HandlerFunc         -- ^ Function to handle incoming messages
         -&gt; IO ()
serveLog port handlerfunc = withSocketsDo $
    do -- Look up the port.  Either raises an exception or returns
       -- a nonempty list.
       addrinfos &lt;- getAddrInfo
                    (Just (defaultHints {addrFlags = [AI_PASSIVE]}))
                    Nothing (Just port)
       let serveraddr = head addrinfos

       -- Create a socket
       sock &lt;- socket (addrFamily serveraddr) Datagram defaultProtocol

       -- Bind it to the address we&#39;re listening to
       bindSocket sock (addrAddress serveraddr)

       -- Loop forever processing incoming data.  Ctrl-C to abort.
       procMessages sock
    where procMessages sock =
              do -- Receive one UDP packet, maximum length 1024 bytes,
                 -- and save its content into msg and its source
                 -- IP and port into addr
                 (msg, _, addr) &lt;- recvFrom sock 1024
                 -- Handle it
                 handlerfunc addr msg
                 -- And process more messages
                 procMessages sock

-- A simple handler that prints incoming packets
plainHandler :: HandlerFunc
plainHandler addr msg =
    putStrLn $ &quot;From &quot; ++ show addr ++ &quot;: &quot; ++ msg
</code></pre>
<p>You can run this in <code>ghci</code>. A call to
<code>serveLog "1514" plainHandler</code> will set up a UDP server on
port 1514 that will use <code>plainHandler</code> to print out every
incoming UDP packet on that port. Ctrl-C will terminate the program.</p>
<div class="NOTE">
<p>In case of problems</p>
<p>Getting <code>bind: permission denied</code> when testing this? Make
sure you use a port number greater than 1024. Some operating systems
only allow the <code>root</code> user to bind to ports less than
1024.</p>
</div>
<h1 data-number="3" id="communicating-with-tcp"><span
class="header-section-number">3</span> Communicating with TCP</h1>
<p>TCP is designed to make data transfer over the Internet as reliable
as possible. TCP traffic is a stream of data. While this stream gets
broken up into individual packets by the operating system, the packet
boundaries are neither known nor relevant to applications. TCP
guarantees that, if traffic is delivered to the application at all, that
it has arrived intact, unmodified, exactly once, and in order.
Obviously, things such as a broken wire can cause traffic to not be
delivered, and no protocol can overcome those limitations.</p>
<p>This brings with it some tradeoffs compared with UDP. First of all,
there are a few packets that must be sent at the start of the TCP
conversation to establish the link. For very short conversations, then,
UDP would have a performance advantage. Also, TCP tries very hard to get
data through. If one end of a conversation tries to send data to the
remote, but doesn't receive an acknowledgment back, it will periodically
re-transmit the data for some time before giving up. This makes TCP
robust in the face of dropped packets. However, it also means that TCP
is not the best choice for real-time protocols that involve things such
as live audio or video.</p>
<h2 data-number="3.1" id="handling-multiple-tcp-streams"><span
class="header-section-number">3.1</span> Handling Multiple TCP
Streams</h2>
<p>With TCP, connections are stateful. That means that there is a
dedicated logical "channel" between a client and server, rather than
just one-off packets as with UDP. This makes things easy for client
developers. Server applications almost always will want to be able to
handle more than one TCP connection at once. How then to do this?</p>
<p>On the server side, you will first create a socket and bind to a
port, just like UDP. Instead of repeatedly listening for data from any
location, your main loop will be around the <code>accept</code> call.
Each time a client connects, the server's operating system allocates a
new socket for it. So we have the <em>master</em> socket, used only to
listen for incoming connections, and never to transmit data. We also
have the potential for multiple <em>child</em> sockets to be used at
once, each corresponding to a logical TCP conversation.</p>
<p>In Haskell, you will usually use <code>forkIO</code> to create a
separate lightweight thread to handle each conversation with a child.
Haskell has an efficient internal implementation of this that performs
quite well.</p>
<h2 data-number="3.2" id="tcp-syslog-server"><span
class="header-section-number">3.2</span> TCP Syslog Server</h2>
<p>Let's say that we wanted to reimplement syslog using TCP instead of
UDP. We could say that a single message is defined not by being in a
single packet, but is ended by a trailing newline character
<code>'\n'</code>. Any given client could send 0 or more messages to the
server using a given TCP connection. Here's how we might write that.</p>
<pre class="example"><code>import Data.Bits
import Network.Socket
import Network.BSD
import Data.List
import Control.Concurrent
import Control.Concurrent.MVar
import System.IO

type HandlerFunc = SockAddr -&gt; String -&gt; IO ()

serveLog :: String              -- ^ Port number or name; 514 is default
         -&gt; HandlerFunc         -- ^ Function to handle incoming messages
         -&gt; IO ()
serveLog port handlerfunc = withSocketsDo $
    do -- Look up the port.  Either raises an exception or returns
       -- a nonempty list.
       addrinfos &lt;- getAddrInfo
                    (Just (defaultHints {addrFlags = [AI_PASSIVE]}))
                    Nothing (Just port)
       let serveraddr = head addrinfos

       -- Create a socket
       sock &lt;- socket (addrFamily serveraddr) Stream defaultProtocol

       -- Bind it to the address we&#39;re listening to
       bindSocket sock (addrAddress serveraddr)

       -- Start listening for connection requests.  Maximum queue size
       -- of 5 connection requests waiting to be accepted.
       listen sock 5

       -- Create a lock to use for synchronizing access to the handler
       lock &lt;- newMVar ()

       -- Loop forever waiting for connections.  Ctrl-C to abort.
       procRequests lock sock

    where
          -- | Process incoming connection requests
          procRequests :: MVar () -&gt; Socket -&gt; IO ()
          procRequests lock mastersock =
              do (connsock, clientaddr) &lt;- accept mastersock
                 handle lock clientaddr
                    &quot;syslogtcpserver.hs: client connected&quot;
                 forkIO $ procMessages lock connsock clientaddr
                 procRequests lock mastersock

          -- | Process incoming messages
          procMessages :: MVar () -&gt; Socket -&gt; SockAddr -&gt; IO ()
          procMessages lock connsock clientaddr =
              do connhdl &lt;- socketToHandle connsock ReadMode
                 hSetBuffering connhdl LineBuffering
                 messages &lt;- hGetContents connhdl
                 mapM_ (handle lock clientaddr) (lines messages)
                 hClose connhdl
                 handle lock clientaddr
                    &quot;syslogtcpserver.hs: client disconnected&quot;

          -- Lock the handler before passing data to it.
          handle :: MVar () -&gt; HandlerFunc
          -- This type is the same as
          -- handle :: MVar () -&gt; SockAddr -&gt; String -&gt; IO ()
          handle lock clientaddr msg =
              withMVar lock
                 (\a -&gt; handlerfunc clientaddr msg &gt;&gt; return a)

-- A simple handler that prints incoming packets
plainHandler :: HandlerFunc
plainHandler addr msg =
    putStrLn $ &quot;From &quot; ++ show addr ++ &quot;: &quot; ++ msg
</code></pre>
<p>For our <code>SyslogTypes</code> implementation, see <a
href="27-sockets-and-syslog.org::*UDP Client Example: syslog">the
section called "UDP Client Example: syslog"</a></p>
<p>Let's look at this code. Our main loop is in
<code>procRequests</code>, where we loop forever waiting for new
connections from clients. The <code>accept</code> call blocks until a
client connects. When a client connects, we get a new socket and the
address of the client. We pass a message to the handler about that, then
use <code>forkIO</code> to create a thread to handle the data from that
client. This thread runs <code>procMessages</code>.</p>
<p>When dealing with TCP data, it's often convenient to convert a socket
into a Haskell <code>Handle</code>. We do so here, and explicitly set
the buffering—an important point for TCP communication. Next, we set up
lazy reading from the socket's <code>Handle</code>. For each incoming
line, we pass it to <code>handle</code>. After there is no more
data—because the remote end has closed the socket—we output a message
about that.</p>
<p>Since we may be handling multiple incoming messages at once, we need
to ensure that we're not writing out multiple messages at once in the
handler. That could result in garbled output. We use a simple lock to
serialize access to the handler, and write a simple <code>handle</code>
function to handle that.</p>
<p>You can test this with the client we'll present next, or you can even
use the <code>telnet</code> program to connect to this server. Each line
of text you send to it will be printed on the display by the server.
Let's try it out:</p>
<pre class="screen"><code>ghci&gt; :load syslogtcpserver.hs
[1 of 1] Compiling Main             ( syslogtcpserver.hs, interpreted )
Ok, modules loaded: Main.
ghci&gt; serveLog &quot;10514&quot; plainHandler
Loading package parsec-2.1.0.0 ... linking ... done.
Loading package network-2.1.0.0 ... linking ... done.
</code></pre>
<p>At this point, the server will begin listening for connections at
port 10514. It will not appear to be doing anything until a client
connects. We could use telnet to connect to the server:</p>
<pre class="screen"><code>~$ telnet localhost 10514
Trying 127.0.0.1...
Connected to localhost.
Escape character is &#39;^]&#39;.
Test message
^]
telnet&gt; quit
Connection closed.
</code></pre>
<p>Meanwhile, in our other terminal running the TCP server, you'll see
something like this:</p>
<pre class="screen"><code>From 127.0.0.1:38790: syslogtcpserver.hs: client connected
From 127.0.0.1:38790: Test message
From 127.0.0.1:38790: syslogtcpserver.hs: client disconnected
</code></pre>
<p>This shows that a client connected from port 38790 on the local
machine (127.0.0.1). After it connected, it sent one message, and
disconnected. When you are acting as a TCP client, the operating system
assigns an unused port for you. This port number will usually be
different each time you run the program.</p>
<h2 data-number="3.3" id="tcp-syslog-client"><span
class="header-section-number">3.3</span> TCP Syslog Client</h2>
<p>Now, let's write a client for our TCP syslog protocol. This client
will be similar to the UDP client, but there are a couple of changes.
First, since TCP is a streaming protocol, we can send data using a
<code>handle</code> rather than using the lower-level socket operations.
Secondly, we no longer need to store the destination address in the
<code>SyslogHandle</code> since we will be using <code>connect</code> to
establish the TCP connection. Finally, we need a way to know where one
message ends and the next begins. With UDP, that was easy because each
message was a discrete logical packet. With TCP, we'll just use the
newline character <code>'\n'</code> as the end-of-message marker, though
that means that no individual message may contain the newline. Here's
our code:</p>
<pre class="example"><code>import Data.Bits
import Network.Socket
import Network.BSD
import Data.List
import SyslogTypes
import System.IO

data SyslogHandle =
    SyslogHandle {slHandle :: Handle,
                  slProgram :: String}

openlog :: HostName             -- ^ Remote hostname, or localhost
        -&gt; String               -- ^ Port number or name; 514 is default
        -&gt; String               -- ^ Name to log under
        -&gt; IO SyslogHandle      -- ^ Handle to use for logging
openlog hostname port progname =
    do -- Look up the hostname and port.  Either raises an exception
       -- or returns a nonempty list.  First element in that list
       -- is supposed to be the best option.
       addrinfos &lt;- getAddrInfo Nothing (Just hostname) (Just port)
       let serveraddr = head addrinfos

       -- Establish a socket for communication
       sock &lt;- socket (addrFamily serveraddr) Stream defaultProtocol

       -- Mark the socket for keep-alive handling since it may be idle
       -- for long periods of time
       setSocketOption sock KeepAlive 1

       -- Connect to server
       connect sock (addrAddress serveraddr)

       -- Make a Handle out of it for convenience
       h &lt;- socketToHandle sock WriteMode

       -- We&#39;re going to set buffering to BlockBuffering and then
       -- explicitly call hFlush after each message, below, so that
       -- messages get logged immediately
       hSetBuffering h (BlockBuffering Nothing)

       -- Save off the socket, program name, and server address in a handle
       return $ SyslogHandle h progname

syslog :: SyslogHandle -&gt; Facility -&gt; Priority -&gt; String -&gt; IO ()
syslog syslogh fac pri msg =
    do hPutStrLn (slHandle syslogh) sendmsg
       -- Make sure that we send data immediately
       hFlush (slHandle syslogh)
    where code = makeCode fac pri
          sendmsg = &quot;&lt;&quot; ++ show code ++ &quot;&gt;&quot; ++ (slProgram syslogh) ++
                    &quot;: &quot; ++ msg

closelog :: SyslogHandle -&gt; IO ()
closelog syslogh = hClose (slHandle syslogh)

{- | Convert a facility and a priority into a syslog code -}
makeCode :: Facility -&gt; Priority -&gt; Int
makeCode fac pri =
    let faccode = codeOfFac fac
        pricode = fromEnum pri
        in
          (faccode `shiftL` 3) .|. pricode
</code></pre>
<p>We can try it out under <code>ghci</code>. If you still have the TCP
server running from earlier, your session might look something like
this:</p>
<pre class="screen"><code>ghci&gt; :load syslogtcpclient.hs
Loading package base ... linking ... done.
[1 of 2] Compiling SyslogTypes      ( SyslogTypes.hs, interpreted )
[2 of 2] Compiling Main             ( syslogtcpclient.hs, interpreted )
Ok, modules loaded: Main, SyslogTypes.
ghci&gt; openlog &quot;localhost&quot; &quot;10514&quot; &quot;tcptest&quot;
Loading package parsec-2.1.0.0 ... linking ... done.
Loading package network-2.1.0.0 ... linking ... done.
ghci&gt; sl &lt;- openlog &quot;localhost&quot; &quot;10514&quot; &quot;tcptest&quot;
ghci&gt; syslog sl USER INFO &quot;This is my TCP message&quot;
ghci&gt; syslog sl USER INFO &quot;This is my TCP message again&quot;
ghci&gt; closelog sl
</code></pre>
<p>Over on the server, you'll see something like this:</p>
<pre class="screen"><code>From 127.0.0.1:46319: syslogtcpserver.hs: client connected
From 127.0.0.1:46319: &lt;9&gt;tcptest: This is my TCP message
From 127.0.0.1:46319: &lt;9&gt;tcptest: This is my TCP message again
From 127.0.0.1:46319: syslogtcpserver.hs: client disconnected
</code></pre>
<p>The <code>&lt;9&gt;</code> is the priority and facility code being
sent along, just as it was with UDP.</p>
</body>
</html>
